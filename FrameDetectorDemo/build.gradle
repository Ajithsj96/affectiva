apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.affectiva.framedetectordemo"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 2
        versionName "3.1.3-01"
        setProperty("archivesBaseName", "$project.name-$versionName-$versionCode")
    }
    buildTypes {
        release {
            minifyEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile('com.affectiva.android:affdexsdk:3.1.3-SNAPSHOT')
}

// build a signed release apk only if the environment is configured
// with the properties that let us access the keystore.  you can put
// them into ~/.gradle/gradle.properties
if (project.hasProperty('releaseKeystoreFile')) {
    android.signingConfigs {
        release {
            storeFile file(releaseKeystoreFile)
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}
